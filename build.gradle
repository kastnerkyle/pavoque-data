buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath group: 'org.yaml', name: 'snakeyaml', version: '1.17'
    }
}

plugins {
    id 'base'
}

import org.yaml.snakeyaml.Yaml
import org.yaml.snakeyaml.DumperOptions

group 'de.dfki.mary'
version '0.2-SNAPSHOT'
description 'The PAVOQUE corpus of expressive speech'

ext {
    styles = ['angry', 'happy', 'neutral', 'poker', 'sad']
    yamlFiles = (styles + ['outtakes']).collect { file("pavoque-${it}.yaml") }
}

subprojects {
    apply plugin: 'base'

    repositories {
        ivy {
            url 'http://mary.dfki.de/download/pavoque-data'
            layout 'pattern', {
                artifact 'Recordings/[artifact].[ext]'
            }
        }
    }

    dependencies {
        'default' name: project.name, ext: 'tar'
    }

    task download(type: Copy) {
        from configurations.default
        into "$rootDir/Recordings"
        rename { "${project.name}.tar" }
    }
}

task yaml << {
    yamlFiles.each { yamlFile ->
        def options = new DumperOptions()
        options.defaultFlowStyle = yamlFile.name =~ /outtakes/ ? DumperOptions.FlowStyle.BLOCK : DumperOptions.FlowStyle.AUTO
        def yaml = new Yaml(options)
        def data = yaml.load(yamlFile.newReader()).collect {
            [prompt: it.prompt, text: it.text, style: it.style, start: it.start, end: it.end] +
                    (yamlFile.name =~ /outtakes/ ? [:] : [segments: it.segments])
        }
        yaml.dump(data, yamlFile.newWriter())
    }
}

task text {
    def textDir = file("$buildDir/text")
    group 'Build'
    description "Extract utterance text as text files into $textDir"
    inputs.files yamlFiles
    outputs.dir textDir
    doFirst {
        textDir.mkdirs()
    }
    doLast {
        yamlFiles.each { yamlFile ->
            new Yaml().load(yamlFile.newReader()).each { utterance ->
                file("$textDir/${utterance.prompt}.txt").text = utterance.text
            }
        }
    }
}

task lab {
    def labDir = file("$buildDir/lab")
    group 'Build'
    description "Extract segments as Xwaves label files into $labDir"
    inputs.files yamlFiles
    outputs.dir labDir
    doFirst {
        labDir.mkdirs()
    }
    doLast {
        yamlFiles.each { yamlFile ->
            new Yaml().load(yamlFile.newReader()).each { utterance ->
                if (utterance.segments) {
                    file("$labDir/$utterance.prompt-${utterance.style}.lab").withWriter { lab ->
                        lab.println '#'
                        utterance.segments.each { segment ->
                            lab.println sprintf("    %s  125 %s", segment.end, segment.lab)
                        }
                    }
                }
            }
        }
    }
}

task wav {
    def wavDir = file("$buildDir/wav")
    group 'Build'
    description "Extract audio as WAV files into $wavDir"
    inputs.files yamlFiles
    outputs.dir wavDir
    doFirst {
        wavDir.mkdirs()
    }
    doLast {
        yamlFiles.each { yamlFile ->
            new Yaml().load(yamlFile.newReader()).each { utterance ->
                exec {
                    commandLine 'sox',
                            file("${yamlFile.name - '.yaml' + '.flac'}"),
                            file("$wavDir/$utterance.prompt-${utterance.style}.wav"),
                            'trim',
                            utterance.start,
                            '=' + utterance.end
                }
            }
        }
    }
}
