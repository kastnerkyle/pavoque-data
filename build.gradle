buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath group: 'org.yaml', name: 'snakeyaml', version: '1.17'
    }
}

plugins {
    id 'base'
    id 'de.undercouch.download' version '3.0.0'
}

group 'de.dfki.mary'
version '0.2-SNAPSHOT'
description 'The PAVOQUE corpus of expressive speech'

ext {
    styles = ['angry', 'happy', 'neutral', 'poker', 'sad']
    yamlFiles = styles.collect { file("pavoque-${it}.yaml") }
}

task download {
    group 'Build'
    description 'Download audio files'
    doLast {
        download {
            src styles.collect { style ->
                "https://github.com/marytts/pavoque-data/releases/download/v0.2/pavoque-${style}.flac"
            }
            dest projectDir
            overwrite false
            compress false
            acceptAnyCertificate true
        }
    }
}

task text(type: ExtractTextTask) {
    description "Extract utterance text as text files into $destDir"
    group 'Build'
}

task lab(type: ExtractLabTask) {
    description "Extract segments as Xwaves label files into $destDir"
    group 'Build'
}

task wav(type: ExtractWavTask) {
    description "Extract audio as WAV files into $destDir"
    group 'Build'
}

class ExtractTask extends DefaultTask {
    @InputFiles
    def yamlFiles = project.yamlFiles

    def yaml = new org.yaml.snakeyaml.Yaml()
}

class ExtractTextTask extends ExtractTask {
    @OutputDirectory
    def destDir = project.file("$project.buildDir/text")

    @TaskAction
    def extract() {
        yamlFiles.each { yamlFile ->
            yaml.load(yamlFile.newReader()).each { utterance ->
                project.file("$destDir/${utterance.prompt}.txt").text = utterance.text
            }
        }
    }
}

class ExtractLabTask extends ExtractTask {
    @OutputDirectory
    def destDir = project.file("$project.buildDir/lab")

    @TaskAction
    def extract() {
        yamlFiles.each { yamlFile ->
            yaml.load(yamlFile.newReader()).each { utterance ->
                if (utterance.segments) {
                    project.file("$destDir/$utterance.prompt-${utterance.style}.lab").withWriter { lab ->
                        lab.println '#'
                        utterance.segments.each { segment ->
                            lab.println sprintf("    %s  125 %s", segment.end, segment.lab)
                        }
                    }
                }
            }
        }
    }
}

class ExtractWavTask extends ExtractTask {
    @OutputDirectory
    def destDir = project.file("$project.buildDir/wav")

    @TaskAction
    def extract() {
        yamlFiles.each { yamlFile ->
            yaml.load(yamlFile.newReader()).each { utterance ->
                project.exec {
                    commandLine 'sox',
                            project.file("${yamlFile.name - '.yaml' + '.flac'}"),
                            project.file("$destDir/$utterance.prompt-${utterance.style}.wav"),
                            'trim',
                            utterance.start,
                            '=' + utterance.end
                }
            }
        }
    }
}
